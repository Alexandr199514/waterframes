import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

base {
    archivesName = modid
    version = mcversion + "-" +  modversion
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(21)
}

println "Gradle Java: ${JavaVersion.current()}"
println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}
runs {
    configureEach {
        systemProperty  'forge.logging.console.level', 'debug'
        systemProperty  'log4j.appender.Console', "org.apache.log4j.ConsoleAppender"
        systemProperty  'terminal.jline', 'true'

        modSource sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', modid
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', modid
        programArgument '--nogui'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    mavenCentral()
    maven { url "https://maven.tterrag.com/" }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "net.neoforged:neoforge:$fgversion"

    //DEPENDENCIES
    implementation fileTree('libs') { include '*.jar' }
    implementation "com.github.WaterMediaTeam.watermedia:build:$watermediaversion"
    implementation "curse.maven:creativecore-257814:$creativecoreversion"

    // UTIL
//    implementation fg.deobf("curse.maven:ferritecore-429235:$ferritecoreversion")
//    implementation fg.deobf("curse.maven:fastload-630620:$fastloadversion")
//    implementation fg.deobf("curse.maven:lazy-dfu-forge-460819:$lazyversion")
//    implementation fg.deobf("curse.maven:spark-361579:$sparkversion")
//    implementation fg.deobf("curse.maven:embeddium-908741:$embeddiumversion")
//    implementation fg.deobf("curse.maven:worldedit-225608:$weversion")
//    implementation fg.deobf("curse.maven:oculus-581495:${oculusversion}")
//    implementation fg.deobf("curse.maven:jei-238222:$jeiversion")

    // MOD SUPPORT
//    implementation fg.deobf("com.simibubi.create:create-$mcversion:$createversion:slim") { transitive = false }
//    implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-$mcversion:$flywheelversion")
//    implementation fg.deobf("com.tterrag.registrate:Registrate:$registrateversion")
}

tasks.processResources.outputs.upToDateWhen { false }
processResources {
    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta', 'resourcepacks/**/pack.mcmeta', "$modid.$mixin_file_suffix".toString()]) {
        expand project.properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes([
            "Specification-Title": modname,
            "Specification-Vendor": modauthor,
            "Specification-Version": modversion,
            "Implementation-Title": modname,
            "Implementation-Version"  : modversion,
            "Implementation-Vendor": modauthor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "$modid.$mixin_file_suffix"
        ])
    }
}

task upload(type: GradleBuild) {
    description 'Uploads new version to modrinth and curseforge.'
    group = 'publishing'
    tasks = [ 'curseforge', 'modrinth']
}

tasks.modrinth.enabled = System.getenv("MODRINTH_TOKEN") != null
tasks.modrinth.group = 'publishing'
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = modnamebig + "/" + mcversion + "/" + modversion
    projectId = modrinthid
    versionNumber = mcversion + "-" + modversion
    versionType = versiontype
    uploadFile = jar
    gameVersions = [ mcversion ]
    changelog = getChangelogText(true)
    loaders = [ "forge", "neoforge" ]
    dependencies {
        required.project "creativecore"
        required.project "watermedia"
        incompatible.project "stellarity"
    }
}

task curseforge(type: TaskPublishCurseForge) {
    disableVersionDetection()

    apiToken = System.getenv("CURSEFORGE_TOKEN") != null ? System.getenv("CURSEFORGE_TOKEN") : "empty"

    // MAIN FILE
    def mainFile = upload(curseforgeid, jar)
    mainFile.displayName = modnamebig + "/" + mcversion + "/" + modversion
    mainFile.changelog = getChangelogText(false)
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addModLoader("NeoForge")
    mainFile.addGameVersion(mcversion, "server", "client")
    mainFile.addRequirement("creativecore", "watermedia")
    mainFile.addIncompatibility("fancyvideo-api", "stellarity")
    mainFile.releaseType = versiontype
}

String getChangelogText(boolean latest) {
    String result = ''
    List<String> lines = file('CHANGELOG.md').readLines('UTF-8')

    for (String line: lines) {
        if (latest && line.isEmpty()) {
            return result
        }

        result += line + "\n"
    }
    return result
}
